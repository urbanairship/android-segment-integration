group = 'com.urbanairship.android'
version = '1.2.1'
description = "Urban Airship Segment Integration"

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 27

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 1
        versionName project.version
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    provided 'com.segment.analytics.android:analytics:4.2.0'
    implementation 'com.urbanairship.android:urbanairship-core:9.7.1'

    testCompile 'junit:junit:4.12'
    testCompile 'com.segment.analytics.android:analytics-tests:4.2.0'
    testCompile 'org.mockito:mockito-core:1.9.5'
    testCompile 'org.robolectric:shadows-support-v4:3.0'
}

// Create the pom configuration:
def pomConfig = {
    licenses {
        license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution 'repo'
        }
    }

    developers {
        developer {
            name 'Urban Airship'
        }
    }

    scm {
        connection 'https://github.com/urbanairship/android-segment-integration.git'
        url 'https://github.com/urbanairship/android-segment-integration'
    }
}

publishing {
    publications {
        Production(MavenPublication) {

            artifact("$buildDir/outputs/aar/" + project.name + "-release.aar")
            groupId project.group
            artifactId project.name

            pom.withXml {
                def root = asNode()

                def dependenciesNode = root.appendNode('dependencies')
                configurations.implementation.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }

                root.appendNode('description', project.description)
                root.appendNode('name', project.name)
                root.appendNode('url', 'https://urbanairship.com')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = "$System.env.BINTRAY_USER"
    key = "$System.env.BINTRAY_API_KEY"

    if (project.hasProperty('bintrayUser') && project.hasProperty('bintrayApiKey')) {
        // Credentials should be stored in ~/.gradle/gradle.properties
        user = bintrayUser
        key = bintrayApiKey
    }

    publications = ['Production']

    pkg {
        repo = 'android'
        userOrg = 'urbanairship'
        name = project.name
        desc = project.description
        websiteUrl = 'https://urbanairship.com'
        licenses = ['Apache-2.0']
        publish = true
        vcsUrl = 'https://github.com/urbanairship/android-segment-integration.git'
        version {
            desc = project.description
            name = project.version
            released = new Date()

            mavenCentralSync {
                user = "$System.env.MAVEN_USER"
                password = "$System.env.MAVEN_PASSWORD"

                if (project.hasProperty('mavenUser') && project.hasProperty('mavenPassword')) {
                    // Credentials should be stored in ~/.gradle/gradle.properties
                    user = mavenUser
                    password = mavenPassword
                }

                sync = true
                close = '1'
            }
        }
    }
}